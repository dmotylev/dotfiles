# vim: ft=zsh:
zmodload -m -F zsh/files b:zf_\* # provides builtin mkdir, ln and other commands with prefix zf_
  
# setup the stub if brew is not installed
if (( $+commands[brew] )) {
    typeset -g BREW=$commands[brew]
} else {
    typeset -fU brew () { 
        df_error "brew not found"
        df_info "Try to install brew with next command:\n\t$_ti[bold]/usr/bin/ruby -e \"\$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/master/install)\""
        return 2
    }
}

# "optimized" 'brew --prefix' will return prefix for any given formula and cache it for the next 24h
typeset -fU brew--prefix () {
    setopt local_options
    setopt extended_glob
	[[ -z $BREW ]] && return 2
	[[ -z $1 ]] && return 1
    local formula=$1 cache_dir="$HOME/.cache/brew--prefix" cache=${1}
    [[ -d $cache_dir ]] || zf_mkdir -m 0700 -p $cache_dir
    if [[ ! -e $cache_dir/$cache || -n $cache_dir/$cache(#qN.mh+24) ]] {
        brew --prefix $formula | tee $cache_dir/$cache
        brew_status=$pipestatus[1]
        [[ $brew_status -ne 0 ]] && zf_rm -s $cache_dir/$cache
        return $brew_status
    }
    df_cat $cache_dir/$cache
}

typeset -fU brew--prefix-purge () { 
    zf_rm -sfr $HOME/.cache/brew--prefix
}

unfunction df_homebrew
