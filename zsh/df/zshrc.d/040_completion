# vim: ft=zsh:
# On slow systems, checking the cached .zcompdump file to see if it must be 
# regenerated adds a noticable delay to zsh startup.  This little hack restricts 
# it to once a day.  It should be pasted into your own completion file.
#
# The globbing is a little complicated here:
# - '#q' is an explicit glob qualifier that makes globbing work within zsh's [[ ]] construct.
# - 'N' makes the glob pattern evaluate to nothing when it doesn't match (rather than throw a globbing error)
# - '.' matches "regular files"
# - 'mh+24' matches files (or directories or whatever) that are older than 24 hours.
autoload -Uz compinit 

function df_completion_setup {
    setopt local_options
    setopt extended_glob

    if [[ -n ${HOME}/.zcompdump(#qN.mh+24) ]] {
        echo "------"
        zf_rm ${HOME}/.zcompdump
    }
    compinit -C
}

df_completion_setup

unfunction df_completion_setup
